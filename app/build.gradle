apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"

static def getProperty(String filename, String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(filename))
    return properties.getProperty(property)
}

static def quoted(String value) {
    return "\"${value}\""
}

ext {
    appName = 'TuneFull'
    appNameSlug = 'tunefull'
    baseUrl = 'https://tunefull.github.io'
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "edu.cnm.deepdive.tunefull"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def connection_properties = "$projectDir/../../../services/tunefullproperties.properties"

        buildConfigField "String", "CLIENT_ID",
                quoted(getProperty(connection_properties, "client_id"))
        buildConfigField "String", "BASE_URL",
                quoted(getProperty(connection_properties, "base_url"))

        buildConfigField "String", "GOOGLE_CLIENT_ID",
                quoted(getProperty(connection_properties, "google_client_id"))
        buildConfigField "String", "GOOGLE_BASE_URL",
                quoted(getProperty(connection_properties, "google_base_url"))


        resValue "string", "client_id", getProperty(connection_properties, "client_id")
        resValue "string", "authorization_scope", getProperty(connection_properties, "authorization_scope")
        resValue "string", "authorization_endpoint_uri", getProperty(connection_properties, "authorization_endpoint_uri")
        resValue "string", "token_endpoint_uri", getProperty(connection_properties, "token_endpoint_uri")

        def redirect_uri_scheme = getProperty(connection_properties, "redirect_uri_scheme")
        def redirect_uri_host = getProperty(connection_properties, "redirect_uri_host")
        def redirect_uri_path = getProperty(connection_properties, "redirect_uri_path")

        resValue "string", "redirect_uri_scheme", redirect_uri_scheme
        resValue "string", "redirect_uri_host", redirect_uri_host
        resValue "string", "redirect_uri_path", redirect_uri_path
        resValue "string", "redirect_uri", "${redirect_uri_scheme}://${redirect_uri_host}${redirect_uri_path}"

        buildConfigField "String", "SPOTIFY_BASE_URL",
                quoted(getProperty(connection_properties, "spotify_base_url"))

        manifestPlaceholders = [
                'appAuthRedirectScheme' : "$redirect_uri_scheme"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }


    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // .jar-based libraries included in project
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Basic Android widgets
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Material Design component dependencies
    implementation 'com.google.android.material:material:1.2.1'

    // Preferences/settings dependencies
    implementation 'androidx.preference:preference:1.1.1'

    // Fragment navigation dependencies.
    implementation 'androidx.navigation:navigation-fragment:2.3.2'
    implementation 'androidx.navigation:navigation-ui:2.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Lifecycle (LiveData and ViewModel) dependencies
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // ReactiveX dependencies
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Gson (Google JSON parser) dependency
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit (REST client) with ReactiveX & Gson integration
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // OkHttp logging dependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Google Sign In dependency
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    //OpenId dependency (Spotify Sign In)
    implementation 'net.openid:appauth:0.7.1'

    // Spotify Remote dependency
    implementation project(':spotify-app-remote')

    // TODO remove this dependency on completion
    // Stetho (inspection) dependency
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Test dependencies
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}

// TODO add other links as appropriate
android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        def docTitle = "${appName} ${project.android.defaultConfig.versionName}"
        def compileTask = variant.javaCompileProvider.get()
        def androidLibJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        title docTitle
        description "Generates Javadoc for $variant.name."
        source = compileTask.source
        destinationDir = file("${projectDir}/../docs/api")
        doFirst {
            classpath = files(compileTask.classpath.files) + files(androidLibJar)
        }
        options {
            links 'https://docs.oracle.com/javase/8/docs/api/'
            links 'http://reactivex.io/RxJava/javadoc/'
            links 'https://javadoc.io/doc/io.reactivex.rxjava2/rxandroid/latest/'
            links 'https://javadoc.io/doc/com.facebook.stetho/stetho/latest/'
            links 'https://commons.apache.org/proper/commons-csv/apidocs/'
            linksOffline 'https://developer.android.com/reference', "${projectDir}/../link-offline/android"
            linksOffline 'https://developers.google.com/android/reference', "${projectDir}/../link-offline/google"
            windowTitle docTitle
        }
        options.addStringOption('Xdoclint:none', '-quiet')
        options.source = '8'
    }
}